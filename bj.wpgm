import persona.*
import textos.*
import wollok.game.*
import carta.*
import constants.*

object juego {
  var property mazoCartas = []
  const palos = ["corazon", "diamante", "pica", "trebol"]
  const property crupier = new Crupier(posicion = new Posicion(x = 50, y = 75))
  const property jugador = new Jugador(posicion = new Posicion(x = 50, y = 25))
  var property turnoJugador = 0
  var property turnoCrupier = false
  var property estaEnJuego = false
  var property puedeJugarJugador = false
  const textoApuesta = new TextoConNumeros(
    positionTexto = game.at(5, 50),
    positionNumeros = game.at(17, 52),
    image = "apuesta.png"
  )
  const textoPuntaje = new TextoConNumeros(
    positionTexto = game.at(20, 25),
    positionNumeros = game.at(32, 25),
    image = "puntaje.png"
  )
  const textoDineroDisponible = new TextoConNumeros(
    positionTexto = game.at(35, 5),
    positionNumeros = game.at(63, 7),
    image = "dinerodisponible.png"
  )
  const textoBotones = new ImagenClase(
    position = game.at(115, 50),
    image = "botones.png"
  )
  
  method repartirCarta(persona) {
    persona.sacarCarta(mazoCartas)
    mazoCartas = mazoCartas.drop(1)
  }
  
  method repartirCartaCrupier() {
    self.repartirCarta(crupier)
  }
  
  method repartirCartaJugador() {
    self.repartirCarta(jugador)
    textoPuntaje.setNumero(jugador.puntaje())
  }
  
  method generarValores() {
    valores.put("as", 1)
    valores.put("j", 10)
    valores.put("q", 10)
    valores.put("k", 10)
    
    (2 .. 10).forEach({ i => valores.put(i, i) })
  }
  
  method agregarMazoAlGeneral() {
    palos.forEach(
      { palo => valores.keys().forEach(
          { numero => mazoCartas.add(new Carta(palo = palo, numero = numero)) }
        ) }
    )
  }
  
  method generarMazo() {
    8.times({ _ => self.agregarMazoAlGeneral() })
    
    mazoCartas.randomize()
  }
  
  method mostrarDineroDisponible() {
    textoDineroDisponible.setNumero(jugador.dinero())
  }
  
  method mostrarApuesta() {
    textoApuesta.setNumero(jugador.apuesta())
  }
  
  method empezarRonda() {
    // primero tienen que apostar todos los jugadores.
    self.mostrarDineroDisponible()
    self.mostrarApuesta()
  }
  
  method modificarApuesta(cantidad) {
    if (estaEnJuego) {
      return
    }
    jugador.aumentarApuesta(cantidad)
    self.mostrarDineroDisponible()
    self.mostrarApuesta()
    return
  }
  
  method hitCard() {
    if (!puedeJugarJugador) {
      return
    }
    self.repartirCartaJugador()
    self.chequearMano()
    return
  }
  
  method chequearMano() {
    if (jugador.puntaje() > 21) {
      console.println("Perdiste") // REEMPLAZAR POR UNA VISUAL
      game.schedule(
        2000,
        { 
          jugador.apuesta(0)
          game.removeVisual(textoPuntaje)
          textoPuntaje.removeVisual()
          jugador.limpiarMano()
          self.mostrarDineroDisponible()
          self.mostrarApuesta()
          estaEnJuego = false
          puedeJugarJugador = false
        }
      )
    }
  }
  
  method iniciarRonda() {
    if ((jugador.apuesta() == 0) || estaEnJuego) {
      return
    }
    estaEnJuego = true
    game.addVisual(textoPuntaje)
    textoPuntaje.setNumero(jugador.puntaje())
    game.schedule(1000, { self.repartirCartaJugador() })
    game.schedule(2000, { self.repartirCartaCrupier() })
    game.schedule(3000, { self.repartirCartaJugador() })
    puedeJugarJugador = true
    
    return
  }
  
  method iniciar() {
    game.start()
    game.height(100)
    game.width(140)
    game.boardGround("fondo.png") // game.addVisual(carta)
    textoDineroDisponible.addVisualTitulo()
    textoApuesta.addVisualTitulo()
    game.addVisual(textoBotones)
    self.generarValores()
    self.generarMazo()
    
    
    keyboard.up().onPressDo({ self.modificarApuesta(5) })
    keyboard.down().onPressDo({ self.modificarApuesta(-5) })
    keyboard.enter().onPressDo({ self.iniciarRonda() })
    keyboard.h().onPressDo({ self.hitCard() })
    
    game.addVisual(mazoVisual)
  }
}

program prueba {
  juego.iniciar()
  
  juego.empezarRonda()
}
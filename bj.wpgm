import persona.*
import textos.*
import wollok.game.*
import carta.*
import constants.*

object juego {
  var property mazoCartas = []
  const palos = ["corazon", "diamante", "pica", "trebol"]
  const property crupier = new Crupier(posicion = new Posicion(x = 50, y = 75))
  const property jugador = new Jugador(posicion = new Posicion(x = 50, y = 25))
  var property turnoJugador = 0
  var property turnoCrupier = false
  var property estaEnJuego = false
  
  method repartirCarta(persona) {
    persona.sacarCarta(mazoCartas)
    mazoCartas = mazoCartas.drop(1)
  }
  
  method repartirCartaCrupier() {
    self.repartirCarta(crupier)
  }
  
  method repartirCartaJugador() {
    self.repartirCarta(jugador)
    textoPuntajeNumeros.text(jugador.puntaje())
  }
  
  method generarValores() {
    valores.put("as", 1)
    valores.put("j", 10)
    valores.put("q", 10)
    valores.put("k", 10)
    
    (2 .. 10).forEach({ i => valores.put(i, i) })
  }
  
  method agregarMazoAlGeneral() {
    palos.forEach(
      { palo => valores.keys().forEach(
          { numero => mazoCartas.add(new Carta(palo = palo, numero = numero)) }
        ) }
    )
  }
  
  method generarMazo() {
    8.times({ _ => self.agregarMazoAlGeneral() })
    
    mazoCartas.randomize()
  }
  
  method mostrarDineroDisponible() {
    textoDineroDisponibleNumeros.text(jugador.dinero())
  }
  
  method mostrarApuesta() {
    textoApuestaNumeros.text(jugador.apuesta())
  }
  
  method empezarRonda() {
    // primero tienen que apostar todos los jugadores.
    self.mostrarDineroDisponible()
    self.mostrarApuesta()
  }
  
  method modificarApuesta(cantidad) {
    if (estaEnJuego) {
      return
    }
    jugador.aumentarApuesta(cantidad)
    self.mostrarDineroDisponible()
    self.mostrarApuesta()
    return
  }
  
  method iniciarRonda() {
    if ((jugador.apuesta() == 0) || estaEnJuego) {
      return
    }
    estaEnJuego = true
    game.addVisual(textoPuntaje)
    textoPuntajeNumeros.text(jugador.puntaje())
    game.schedule(1000, { self.repartirCartaJugador() })
    game.schedule(2000, { self.repartirCartaCrupier() })
    game.schedule(3000, { self.repartirCartaJugador() })
    
    return
  }
  
  method iniciar() {
    game.start()
    game.height(100)
    game.width(140)
    game.boardGround("fondo.png") // game.addVisual(carta)
    game.addVisual(textoDineroDispoible)
    game.addVisual(textoApuesta)
    game.addVisual(textoBotones)
    self.generarValores()
    self.generarMazo()
    
    
    keyboard.up().onPressDo({ self.modificarApuesta(5) })
    keyboard.down().onPressDo({ self.modificarApuesta(-5) })
    keyboard.enter().onPressDo({ self.iniciarRonda() })
    
    game.addVisual(mazoVisual)
  }
}

program prueba {
  juego.iniciar()
  
  juego.empezarRonda()
}